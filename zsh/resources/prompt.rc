#!/bin/zsh

autoload -Uz vcs_info

zstyle ':vcs_info:*' stagedstr ' %F{28}■'
zstyle ':vcs_info:*' unstagedstr ' %F{11}■'
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}: %F{11}%r'
zstyle ':vcs_info:*' enable git svn

precmd ()
{
    if [[ -z $(git ls-files --other --exclude-standard 2> /dev/null) ]] {
        zstyle ':vcs_info:*' formats '[%F{green}%b%c%u%F{red}]%f'
    } else {
        zstyle ':vcs_info:*' formats '[%F{green}%b%c%u %F{red}■%F{red}]%f'
    }
    vcs_info

    local TERMWIDTH
    (( TERMWIDTH = ${COLUMNS} - 1 ))
    PR_FILLBAR=""
    PR_PWDLEN=""

    local promptsize=${#${(%):---( %~ :: %m )---( %D{%H:%M} )---}}
    local pwdsize=${#${(%):-}}

    if [[ "$promptsize + $pwdsize" -gt $TERMWIDTH ]]; then
	((PR_PWDLEN=$TERMWIDTH - $promptsize))
    else
	PR_FILLBAR="\${(l.(($TERMWIDTH - ($promptsize + $pwdsize)))..${PR_HBAR}.)}"
    fi

    #print dir name in term title
    case $TERM in
	xterm*|rxvt|Eterm)
	print -Pn "\e]0;%n@%m: %~\a"
	;;
    esac
}

setprompt ()
{
    setopt prompt_subst
    autoload colors
    colors

    for color in WHITE RED GREEN YELLOW BLACK BLUE MAGENTA CYAN BLUE; do
	eval $color='%{$termcap[md]$fg[${(L)color}]%}'
	eval LIGHT_$color='%{$fg[${(L)color}]%}'
	(( count = $count + 1 ))
    done
    DEF="%{$termcap[me]%}"

    typeset -A altchar
    set -A altchar ${(s..)termcap[ac]}
    PR_SET_CHARSET="%{$termcap[eA]%}"
    PR_SHIFT_IN="%{$termcap[as]%}"
    PR_SHIFT_OUT="%{$termcap[ae]%}"
    PR_HBAR=${altchar[q]:--}
    PR_ULCORNER=${altchar[l]:--}
    PR_LLCORNER=${altchar[m]:--}
    PR_LRCORNER=${altchar[j]:--}
    PR_URCORNER=${altchar[k]:--}

    MCOLOR=$CYAN
    PROMPT='$PR_SET_CHARSET\
$RED$PR_SHIFT_IN$PR_ULCORNER$RED$PR_HBAR$PR_SHIFT_OUT(\
$MCOLOR $WHITE%m$MCOLOR :: $WHITE%~$MCOLOR%$PR_PWDLEN<...<%<<\
$RED )$PR_SHIFT_IN$PR_HBAR$RED$PR_HBAR${(e)PR_FILLBAR}\
$PR_HBAR$RED$PR_HBAR$PR_SHIFT_OUT($WHITE %D{%H:%M} \
$RED)$PR_SHIFT_IN$PR_HBAR$RED$PR_URCORNER$PR_SHIFT_OUT \
$PR_SHIFT_IN$PR_LLCORNER$RED$PR_HBAR$PR_SHIFT_OUT${vcs_info_msg_0_}$YELLOW %(!.#.$)$DEF '

    RPROMPT=' $RED$PR_SHIFT_IN$PR_HBAR$RED$PR_HBAR$PR_SHIFT_OUT\
($MCOLOR%(?,${GREEN}OK,$RED$WHITE%139(?,Segfault,\
%130(?,Interrupt,%138(?,Bus Error,%?)))$RED KO)\
$RED)$PR_SHIFT_IN$PR_HBAR$RED$RED$PR_LRCORNER$PR_SHIFT_OUT$DEF'

    SPROMPT='zsh: correct $MCOLOR%R$DEF to $MCOLOR%r$DEF%b ? ([${MCOLOR}Y$DEF]es/[${MCOLOR}N$DEF]o/[${MCOLOR}E$DEF]dit/[${MCOLOR}A$DEF]bort) '

    PS2='$RED$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$BLUE$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT(\
$MCOLOR%_$BLUE)$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$RED$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT$DEF '
}

LISTPROMPT=''
setprompt
